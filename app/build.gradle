import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id("org.jetbrains.compose")
}

android {
    namespace 'de.danotter.smooothweather'
    compileSdk 34

    defaultConfig {
        applicationId "de.danotter.smooothweather"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Allow anyone to build releases.
            signingConfig = signingConfigs.debug
        }
    }
    buildFeatures {
        buildConfig true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'

        freeCompilerArgs += [
                "-opt-in=kotlin.RequiresOptIn",
                // Enable experimental coroutines APIs, including Flow
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-opt-in=kotlinx.coroutines.FlowPreview",
                "-opt-in=kotlin.Experimental",
        ]
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    coreLibraryDesugaring libs.desugaring
    implementation project(":shared")
    implementation libs.androidx.core
    implementation libs.lifecycle.ktx
    implementation(platform(libs.compose.bom))
    implementation libs.activity.compose
    implementation libs.compose.uiToolingPreview

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.compose.uiTestJunit4
    debugImplementation libs.compose.uiTooling
    debugImplementation libs.compose.testManifest


    implementation libs.hilt
    kapt libs.hilt.compiler
    implementation libs.kotlinx.datetime
    implementation libs.timber
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}